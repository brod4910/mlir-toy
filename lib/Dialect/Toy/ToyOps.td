include "ToyDialect.td"

include "mlir/IR/OpBase.td"
include "mlir/IR/SymbolInterfaces.td"
include "mlir/Interfaces/SideEffectInterfaces.td"

class Toy_Op<string mnemonic, list<Trait> traits = []> : Op<Toy_Dialect, mnemonic, traits>;

def ConstantOp : Toy_Op<"constant"> {
    // Provide a summary and description for this operation. This can be used to
    // auto-generate documentation of the operations within our dialect.
    let summary = "constant operation";
    let description = [{
        Constant operation turns a literal into an SSA value. The data is attached
        to the operation as an attribute. For example:

        %0 = "toy.constant"()
            { value = dense<[[1.0, 2.0, 3.0], [4.0, 5.0, 6.0]]> : tensor<2x3xf64> }
            : () -> tensor<2x3xf64>
    }];

    // The constant operation takes an attribute as the only input.
    // `F64ElementsAttr` corresponds to a 64-bit floating-point ElementsAttr.
    let arguments = (ins F64ElementsAttr:$value);

    // The generic call operation returns a single value of TensorType.
    // F64Tensor corresponds to a 64-bit floating-point TensorType.
    let results = (outs F64Tensor);

    // Add additional verification logic to the constant operation. Setting this bit
    // to `1` will generate a `::mlir::LogicalResult verify()` declaration on the
    // operation class that is called after ODS constructs have been verified, for
    // example the types of arguments and results. We implement additional verification
    // in the definition of this `verify` method in the C++ source file. 
    let hasVerifier = 1;

    // Add custom build methods for the constant operation. These methods populate
    // the `state` that MLIR uses to create operations, i.e. these are used when
    // using `builder.create<ConstantOp>(...)`.
    let builders = [
        // Build a constant with a given constant tensor value.
        OpBuilder<(ins "DenseElementsAttr":$value), [{
        // Call into an autogenerated `build` method.
        build(builder, result, value.getType(), value);
        }]>,

        // Build a constant with a given constant floating-point value. This builder
        // creates a declaration for `ConstantOp::build` with the given parameters.
        OpBuilder<(ins "double":$value)>
    ];
}

